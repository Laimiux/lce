apply from: 'dependencies.gradle'
apply plugin: "org.jetbrains.dokka"

buildscript {
    ext {
        kotlinVersion = '1.9.20'

        versions = [:]
        libraries = [:]
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.4.32"
        classpath "com.vanniktech:gradle-maven-publish-plugin:0.14.2"
    }
}

allprojects {
    repositories {
        mavenCentral()
    }

    tasks.withType(org.jetbrains.dokka.gradle.DokkaTaskPartial).configureEach {
        dokkaSourceSets.named("main") {
            configuration {
                jdkVersion.set(17)
                skipDeprecated.set(true)
                skipEmptyPackages.set(true)
                reportUndocumented.set(false)
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: "com.vanniktech.android.junit.jacoco"
junitJacoco {
    // WARNING! Don't upgrade unless https://github.com/Malinskiy/danger-jacoco/issues/10 is fixed
    jacocoVersion = '0.8.12'
    excludes = [
            // Defaults
            '**/androidx/**/*.*',
            '**/*$$*',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*$Lambda$*.*', // Jacoco can not handle several "$" in class name.
            '**/*$inlined$*.*', // Kotlin specific, Jacoco can not handle several "$" in class name.
            '**/com/laimiux/lce/test/**/*.*'
    ]
    ignoreProjects = [
            "samples/*"
    ]
    includeNoLocationClasses = true
    includeInstrumentationCoverageInMergedReport = true
}
apply from: 'gradle/merge-reports.gradle'
